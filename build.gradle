buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
	// Load config.  It shall from now be referenced as simply config or project.config
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.mod_version}"
group= "exnihilo"
archivesBaseName = "Ex-Nihilo"

minecraft {
    version = "${config.mc_version}-${config.forge_version}"
    runDir = "../run/assets"
	
	//replace version number in code, so that you only have to change it here
    replaceIn "data/ModData.java"
	replace "@EN_VERSION@", project.version
}

repositories {
    maven {
        name = "Player"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        name = "Chicken-Bones"
        url = "http://chickenbones.net/maven"
    }
    maven {
        name = "ProfMobius"
        url = "http://mobiusstrip.eu/maven"
    }
}

dependencies {
	compile "net.industrial-craft:industrialcraft-2:${config.ic2_version}:dev"
    compile "net.sengir.forestry:forestry_1.7.10:${config.forestry_version}:dev"
    compile "appeng:appliedenergistics2:${config.ae2_version}:dev" //NOTE You will need to add "-Dfml.coreMods.load=appeng.transformer.AppEngCore" to your VM Arguments for AE2 to run properly.
    compile "mcp.mobius.waila:Waila:${config.waila_version}"
}

task copyChicken(type: Copy, dependsOn: "extractUserDev") {
    from { configurations.compile }
    include "**/*Chicken*.jar", "**/*NotEnoughItems*.jar"
    exclude "**/CodeChickenLib*" // because CCC downloads it anyways.. -_-
    into file(minecraft.runDir + "/../mods") // paralell to the assets dir

    mustRunAfter "deobfBinJar"
    mustRunAfter "repackMinecraft"
}
tasks.setupDevWorkspace.dependsOn copyChicken
tasks.setupDecompWorkspace.dependsOn copyChicken